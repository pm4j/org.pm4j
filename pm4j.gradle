import groovy.util.slurpersupport.GPathResult

if (!hasProperty('artifactoryLibrariesRepo')) ext.artifactoryLibrariesRepo = 'thirdparty-release'
repositories {
    maven { url "${artifactoryUrl}/${artifactoryLibrariesRepo}" }
    mavenLocal()
    mavenCentral()
}

apply plugin: 'maven'
apply plugin: 'java'

File pomFile = file('../pom.xml')
assert pomFile.exists() : "The file $pomFile does not exist"
GPathResult pomXml = new XmlSlurper().parse(pomFile)
group = pomXml.groupId.text()
version = pomXml.version.text()

sourceCompatibility = 1.6
targetCompatibility = 1.6

// todo: exclude package.html files
sourceSets { main { resources { srcDir 'src/main/java' } } }
sourceSets { test { resources { srcDir 'src/test/java' } } }

// generate sources artifact
task packageSources(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource

}
artifacts.archives packageSources

// generate tests artifact
task packageTests(type: Jar) {
    from sourceSets.test.output
    classifier = 'tests'
}
artifacts.archives packageTests

// add slf4j version variable to each project so that it can be reference by slf4j-api and slf4j-simple artifacts
ext.SLF4J_VERSION='1.7.10'
dependencies {
    // Note that the testCompile dependencies are never exported
    // and are therefore added as dependencies here to each sub-project, individually.
    testCompile group: 'junit', name: 'junit', version: '4.11'
    if(!project.hasProperty('OMIT_PM4J_LOGGER_IMPLEMENTATION') || project.OMIT_PM4J_LOGGER_IMPLEMENTATION==false) {
        testRuntime group: 'org.slf4j', name: 'slf4j-log4j12', version: SLF4J_VERSION
    }
}
